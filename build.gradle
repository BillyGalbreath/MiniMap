plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'com.modrinth.minotaur' version '1.2.1'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
project.build_num = System.getenv("GITHUB_RUN_NUMBER") ?: "SNAPSHOT"
version = project.minecraft_version + "-" + project.build_num
group = project.maven_group

repositories {
    mavenCentral()
    maven {
        url "https://maven.terraformersmc.com/"
    }
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(17)
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

loom {
    mixin.defaultRefmapName = "minimap.refmap.json"
}

import com.modrinth.minotaur.TaskModrinthUpload

task modrinth(type: TaskModrinthUpload) {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "mCuJmbvh"
    versionName = project.minecraft_version + " build " + project.build_num
    versionNumber = project.version
    uploadFile = remapJar
    addGameVersion(project.minecraft_version)
    addLoader('fabric')
    dependencies = []
    changelog = System.getenv("COMMIT_MESSAGE")
}
